/**
 *  @See https://github.com/gradle-nexus/publish-plugin
 *
 */
plugins {
    id("io.github.gradle-nexus.publish-plugin") version "1.2.0"
}

apply {
    plugin "base"
    plugin "java"
    plugin "checkstyle"
    plugin "jacoco"
    plugin "idea"
    plugin "eclipse"
    plugin "maven-publish"
    plugin "signing"
}

apply from: 'variable.gradle'

wrapper {
  gradleVersion = '8.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = JavaVersion.VERSION_1_8
group = GROUP_ID
archivesBaseName = ARTIFACT_ID
version = VERSION
description = DESCRIPTION

ext {
    checkStyleToolVersion = "6.19"
    jacocoToolVersion = "0.7.9"
    commonsLang3Version = "3.7"
    junitVersion = "4.12"
    assertjVersion = "3.9.1"
    guavaVersion = "24.0-jre"
}

dependencies {
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertjVersion"
    testImplementation "com.google.guava:guava:$guavaVersion"
    testImplementation "org.hamcrest:hamcrest-core:2.2"
    testImplementation "org.hamcrest:hamcrest-library:2.2"
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

// Needed to avoid including all the jar signing and sonatype setup.
if (project.hasProperty("withDeployment")) {
    apply from: "$rootProject.projectDir/gradle/sonatype.gradle"
}

checkstyle {
    configFile = file("${project.projectDir}/extra/checkstyle/checkstyle.xml")
    toolVersion = "$checkStyleToolVersion"
    checkstyleTest.enabled = false
}

checkstyleMain {
    enabled = false
    source = 'src/main/java'
    doLast {
        project.ext.checkType = "main"
    }
}

jacoco {
    toolVersion = "$jacocoToolVersion"
}

jacocoTestReport {
    reports {
//        xml.enabled false
//        csv.enabled false
    }
}

nexusPublishing {
    repositories {
        sonatype {
            // Central Portal에서는 기본 OSSRH URL 사용
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = CENTRAL_PORTAL_USERNAME
            password = CENTRAL_PORTAL_PASSWORD
        }
    }
}

// @See https://github.com/gradle-nexus/publish-plugin#add-metadata
publishing {
    publications {
        javaGrokMaven(MavenPublication) {
            groupId = GROUP_ID
            artifactId = ARTIFACT_ID
            version = VERSION

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = ARTIFACT_ID
                description = DESCRIPTION
                url = PROJECT_URL
                licenses {
                    license {
                        name = LICENSE_NAME
                        url = LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id = DEVELOPER_ID
                        name = DEVELOPER_NAME
                        email = DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection = SCM
                    developerConnection = SCM
                    url = SCM
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = CENTRAL_PORTAL_USERNAME
                password = CENTRAL_PORTAL_PASSWORD
            }
        }
    }
}

signing {
    useGpgCmd()
    sign(publishing.publications["javaGrokMaven"])
}

// Central Portal용 체크섬 생성 태스크
task generateChecksums {
    dependsOn publishToMavenLocal

    doLast {
        def repoDir = file("${System.getProperty('user.home')}/.m2/repository/io/github/whatap/${ARTIFACT_ID}/${VERSION}")

        repoDir.listFiles().each { file ->
            if (file.isFile() && !file.name.endsWith('.md5') && !file.name.endsWith('.sha1')) {
                // MD5 생성
                def md5 = java.security.MessageDigest.getInstance('MD5')
                file.eachByte(4096) { bytes, len -> md5.update(bytes, 0, len) }
                new File(file.path + '.md5').text = md5.digest().encodeHex().toString()

                // SHA1 생성
                def sha1 = java.security.MessageDigest.getInstance('SHA-1')
                file.eachByte(4096) { bytes, len -> sha1.update(bytes, 0, len) }
                new File(file.path + '.sha1').text = sha1.digest().encodeHex().toString()

                println "체크섬 생성: ${file.name}"
            }
        }
    }
}

// Central Portal bundle 생성 태스크
task createCentralPortalBundle(type: Zip) {
    dependsOn generateChecksums

    archiveFileName = "${ARTIFACT_ID}-${VERSION}-bundle.zip"
    destinationDirectory = file("$buildDir/central-portal")

    // Maven 디렉토리 구조 유지: io/github/whatap/java-grok/0.1.0/
    from("${System.getProperty('user.home')}/.m2/repository/io/github/whatap/${ARTIFACT_ID}/${VERSION}") {
        include "*"
        exclude "maven-metadata-local.xml"
        exclude "_remote.repositories"
        into "io/github/whatap/${ARTIFACT_ID}/${VERSION}"
    }

    doLast {
        println ""
        println "========================================"
        println "Central Portal Bundle 생성 완료!"
        println "========================================"
        println "파일: ${destinationDirectory.get()}/${archiveFileName.get()}"
        println ""
        println "업로드 방법:"
        println "1. https://central.sonatype.com 접속"
        println "2. Publish Component 클릭"
        println "3. 위 bundle.zip 파일 업로드"
        println "========================================"
    }
}
